plugins {
    id 'java-library'
    id 'application'
    id 'com.google.cloud.tools.jib' version '0.9.10'
}

group 'se.billy'
version '1.0-SNAPSHOT'

ext.moduleName = 'musicserver'
mainClassName = 'musicserver/se.billy.Main'

repositories {
    jcenter()
}

dependencies {
    compile 'io.javalin:javalin:2.0.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'org.slf4j:slf4j-simple:1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}

startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
        classpath = files()
        defaultJvmOpts = [
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                '--module-path', 'APP_HOME_LIBS',
                '--module', mainClassName
        ]
    }
}


afterEvaluate {
    compileJava {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }
}

jib {
    from {
        image = 'openjdk:11-jre-slim'
    }
    to {
        image = 'billy/jib-azure'
    }
    container {
        ports = ['8080']
    }
}
